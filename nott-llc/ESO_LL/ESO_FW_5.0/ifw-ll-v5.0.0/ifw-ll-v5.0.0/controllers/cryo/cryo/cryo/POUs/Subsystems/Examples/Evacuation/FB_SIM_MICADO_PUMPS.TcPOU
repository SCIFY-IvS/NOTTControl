<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.10">
  <POU Name="FB_SIM_MICADO_PUMPS" Id="{e34ea3d5-f755-420e-95b8-32ab710aaa31}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SIM_MICADO_PUMPS
VAR_INPUT
	in_RefPVP:	REFERENCE TO FB_CRYO_PUMP_ECODRY;
	in_RefTMP:	REFERENCE TO FB_CRYO_PUMP_ATH2303M;
	in_Delay:	TIME := T#10S;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	trgStart_PVP:	R_TRIG;
	trgStart_TMP:	R_TRIG;
	trgStop_PVP:	F_TRIG;
	trgStop_TMP:	F_TRIG;
	
	timer_PVP:		TON;
	timer_TMP:		TON;
	
	bON_PVP:		BOOL := FALSE;
	bON_TMP:		BOOL := FALSE;
	
	q_PVP_Running	AT %Q*:	BOOL := TRUE;	// Active low!
	q_TMP_Ready		AT %Q*:	BOOL;
	q_TMP_Speed100	AT %Q*:	BOOL;
	
	ptrTMP_Speed:	POINTER TO DINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Assign pointer to TMP speed.
ptrTMP_Speed	:= ADR(in_RefTMP.stat.nPumpSpeed);

// Get Start signals
trgStart_PVP(CLK:=in_RefPVP.in_bEnable);
trgStart_TMP(CLK:=in_RefTMP.in_bEnable);

// Get Stop signals
trgStop_PVP(CLK:=in_RefPVP.in_bEnable);
trgStop_TMP(CLK:=in_RefTMP.in_bEnable);

// Start timer on Start/Stop events
IF trgStart_PVP.Q THEN
	bON_PVP	:= TRUE;
	timer_PVP(IN:=FALSE);
	timer_PVP(IN:=TRUE, PT:=in_Delay/2);
ELSIF trgStop_PVP.Q THEN
	bON_PVP	:= FALSE;
	timer_PVP(IN:=FALSE);
	timer_PVP(IN:=TRUE, PT:=in_Delay/2);
END_IF

IF trgStart_TMP.Q THEN
	bON_TMP	:= TRUE;
	timer_TMP(IN:=FALSE);
	timer_TMP(IN:=TRUE, PT:=in_Delay);
ELSIF trgStop_TMP.Q THEN
	bON_TMP	:= FALSE;
	timer_TMP(IN:=FALSE);
	timer_TMP(IN:=TRUE, PT:=in_Delay * 2);
END_IF

// Execute timers
timer_PVP();
timer_TMP();

// Set outputs on timer expiry
IF timer_PVP.Q THEN
	// NOTE: PVP feedback is active low.
	IF bON_PVP THEN
		q_PVP_Running	:= FALSE;
	ELSE
		q_PVP_Running	:= TRUE;
	END_IF
END_IF

IF bON_TMP  AND (NOT timer_TMP.Q AND timer_TMP.ET > T#3S) THEN
	// Starting up.
	// Set speed > 100 rpm after 3 sec.
	ptrTMP_Speed^	:= 500;
	q_TMP_Speed100	:= TRUE;
END_IF

IF NOT bON_TMP  AND (NOT timer_TMP.Q AND timer_TMP.ET > T#5S) THEN
	// Stopping the pump.
	// Not ready after 5 sec.
	// However, the pump is still spinning.
	ptrTMP_Speed^	:= 1800;
	q_TMP_Ready		:= FALSE;
END_IF

IF timer_TMP.Q THEN
	IF bON_TMP THEN
		ptrTMP_Speed^	:= 2000;
		q_TMP_Ready	:= TRUE;
	ELSE
		ptrTMP_Speed^	:= 0;
		q_TMP_Ready		:= FALSE;
		q_TMP_Speed100	:= FALSE;
	END_IF
END_IF]]></ST>
    </Implementation>
    <LineIds Name="FB_SIM_MICADO_PUMPS">
      <LineId Id="133" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="130" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="40" Count="1" />
      <LineId Id="44" Count="2" />
      <LineId Id="43" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="47" Count="9" />
      <LineId Id="24" Count="0" />
      <LineId Id="61" Count="1" />
      <LineId Id="64" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="68" Count="0" />
      <LineId Id="70" Count="1" />
      <LineId Id="69" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="113" Count="1" />
      <LineId Id="134" Count="1" />
      <LineId Id="124" Count="1" />
      <LineId Id="141" Count="1" />
      <LineId Id="127" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="138" Count="1" />
      <LineId Id="129" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="123" Count="0" />
      <LineId Id="74" Count="1" />
      <LineId Id="81" Count="0" />
      <LineId Id="76" Count="1" />
      <LineId Id="82" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="140" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="72" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>